/**
 * ChaiLove: Testing Framework
 */
global testNames = [
	"graphics_print",
	"graphics_draw",
	"graphics_point",
	"mouse_getPosition",
	"graphics_print_font"
]
global tests = []
global currenttest = 0

def conf(t) {
	t.window.width = 460
	t.window.height = 320
	t.console = true
	t.version = "0.20.0"
}

def load() {
	// Set the background to grey, so that we can see screen size.
	love.graphics.setBackgroundColor(50, 50, 50)

	// Index the test cases.
	for (name : testNames) {
		love.filesystem.load(name + ".chai");
		tests.push_back(eval(name + "()"));
	}

	// Load all the tests.
	for (test : tests) {
		test.load();
	}

	love.window.showMessageBox("Hello World!")
}

def draw() {
	tests[currenttest].draw();

	// Use the default font.
	love.graphics.setFont()
	love.graphics.setColor(255, 255, 255)
	love.graphics.print("Test " + to_string(currenttest + 1) + "/" + to_string(tests.size()) + ": " + tests[currenttest].name, 5, 5);
	love.graphics.print("FPS " + to_string(love.timer.getFPS()), 5, 20);

	love.graphics.print("Username: " + love.system.getUsername(), 5, 35)
}

def update(delta) {
	tests[currenttest].update(delta)

	if (love.keyboard.isDown("q")) {
		love.event.quit()
	}
}

def joystickpressed(joystick, button) {
	if (button == "left" || button == "down") {
		currenttest = currenttest - 1
		if (currenttest < 0) {
			currenttest = tests.size() - 1
		}
	}
	else if (button == "right" || button == "up") {
		currenttest = currenttest + 1
		if (currenttest >= tests.size()) {
			currenttest = 0
		}
	}
}

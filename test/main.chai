/**
 * ChaiLove: Testing Framework
 */
global testNames = [
	"love.graphics.print",
	"love.graphics.print.font",
	"love.graphics.draw",
	"love.graphics.point",
	"love.mouse.getPosition",
	"love.audio.play",
	"love.graphics.draw.scaling"
]
global tests = []
global currenttest = 0

def load() {
	print("------------------")
	print("------------------")
	print("- ChaiLove Tests -")
	print("------------------")
	print("------------------")

	// Set the background to grey, so that we can see screen size.
	love.graphics.setBackgroundColor(50, 50, 50)

	// Index the test cases.
	for (name : testNames) {
		love.filesystem.load(name + ".chai");
		var className = name.replace('.', '_')
		print(className)
		tests.push_back(eval(className + "()"));
	}

	// Load all the tests.
	for (test : tests) {
		test.load();
	}

	love.window.showMessageBox("Hello World!")
}

def exit() {
	print("Thanks for playing. Please play again soon!")
}

def draw() {
	tests[currenttest].draw();

	// Use the default font.
	love.graphics.setFont()
	love.graphics.setColor(255, 255, 255)
	love.graphics.print("Test " + to_string(currenttest + 1) + "/" + to_string(tests.size()) + ": " + testNames[currenttest], 5, 5);
	love.graphics.print("FPS " + to_string(love.timer.getFPS()), 5, 20);

	love.graphics.print("Username: " + love.system.getUsername(), 5, 35)
}

def update(delta) {
	tests[currenttest].update(delta)

	if (love.keyboard.isDown("q")) {
		love.event.quit()
	}
}

def gamepadpressed(joystick, button) {
	if (button == "dpleft" || button == "dpdown") {
		currenttest = currenttest - 1
		if (currenttest < 0) {
			currenttest = tests.size() - 1
		}
	}
	else if (button == "dpright" || button == "dpup") {
		currenttest = currenttest + 1
		if (currenttest >= tests.size()) {
			currenttest = 0
		}
	}
}
